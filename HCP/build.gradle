apply plugin: 'com.android.application'


repositories {
    flatDir {
        dir 'libs'
    }
}


//获取时间戳
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmm')
    return formattedDate
}


android {


    productFlavors {  //所有需要打包的渠道,debug时带的名字会取字母升序排序第一个flavors
        baidu {}             //百度
        official {}          //官网
        qh360 {}             //奇虎360
        qq {}                //应用宝
        huawei {}            //华为
    }
    //添加如下代码
    productFlavors.all { flavors->
        flavors.manifestPlaceholders=[CHANNEL_VALUE:name]
    }

    dataBinding{
        enabled=true;
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'LICENSE.txt'
    }


    sourceSets {
        main() {
            jniLibs.srcDirs = ['libs']
            java.srcDirs = ['src/main/java', 'src/main/java-greendao-gen']//第二个是greendao的

        }

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        // your debug keystore
        debug{
            storeFile file("qdong.keystore")
            storePassword 'qdong365'
            keyAlias 'qdongkey'
            keyPassword 'Qdong36523H'

        }
        release{
            storeFile file("qdong.keystore")
            storePassword 'qdong365'
            keyAlias 'qdongkey'
            keyPassword 'Qdong36523H'

        }
    }

    defaultConfig {

        applicationId "com.qdong.hcp"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"


    }

    buildTypes {



        debug{
            manifestPlaceholders=[CHANNEL_VALUE:"debug"]
            buildConfigField "String", "DB_NAME", "\"ngqj_db\""
            buildConfigField "Boolean", "LOG_SWITCH", "true"
            minifyEnabled false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "String", "DB_NAME", "\"ngqj_db\""
            buildConfigField "Boolean", "LOG_SWITCH", "false"
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }


    //修改生成的apk名字
    applicationVariants.all{ variant->
        variant.outputs.each { output->
            def oldFile = output.outputFile
            def appName="HCP_v";
            def fileWholeName = '';
            if(variant.buildType.name.equals('release')){//发布版才改
//                println(variant.productFlavors[0].name)
                // HCP_v1.0_baidu_signed_201608271225.apk
                def releaseApkName = appName + defaultConfig.versionName + '_' + variant.productFlavors[0].name + '_release_signed_'+ getDate() + '.apk'
                output.outputFile = new File(oldFile.parent+"/build/outputs/apk/release_apks", releaseApkName)
            }
            if(variant.buildType.name.equals('beta')){//beta版
                fileWholeName = appName  + defaultConfig.versionName + "_build_" + getDate() + ".apk";
                output.outputFile = new File(oldFile.parent, fileWholeName)
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile project(':qdong_communal_library')
    compile 'com.github.florent37:viewanimator:1.0.0@aar'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'de.greenrobot:greendao:1.3.7'
    compile 'cn.bingoogolapple:bga-badgeview:1.1.3'
    compile 'com.google.android.gms:play-services-appindexing:8.1.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
}
